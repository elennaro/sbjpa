buildscript {
    ext {
        springBootVersion = '1.3.2.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.moowork.gradle:gradle-node-plugin:0.11")
        classpath('com.moowork.gradle:gradle-gulp-plugin:0.11')
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

apply plugin: "com.moowork.node"
apply plugin: 'com.moowork.gulp'

jar {
    baseName = 'sbjpa'
    version = '0.0.1-SNAPSHOT'
}
//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

repositories {
    jcenter()
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

node {
    version = '4.3.0'
    npmVersion = '3.5.1'
    distBaseUrl = 'http://nodejs.org/dist'
    download = true
    workDir = file("${project.buildDir}/nodejs")
    nodeModulesDir = file("${project.projectDir}")
}

// exclude commons-logging
configurations.all {
    exclude group: "commons-logging", module: "commons-logging"
}


dependencies {
    compile("org.slf4j:jcl-over-slf4j:1.7.13");
    compile("ch.qos.logback:logback-classic:1.1.3");
    compile("com.h2database:h2")
    compile("org.hibernate:hibernate-validator")
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    //Webjars
    compile("org.webjars:bootstrap:3.3.6")
    compile("org.webjars:angular-ui-bootstrap:1.0.3")
}

//make sure node and gulp are installed before calling the build task in gulpfile.js
init.dependsOn('installGulp')
init.dependsOn('npmInstall')
clean.dependsOn('installGulp')
clean.dependsOn('npmInstall')
clean.dependsOn('gulp_clean')

//make sure gulp generates CSS for the build
bootRun.dependsOn(gulp_build)
build.dependsOn(gulp_build)

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
